buildscript {
    ext.kotlin_version = '1.3.10'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.8.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.9'
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api 'com.typesafe:config:1.3.3'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.1.8'
    testImplementation 'com.atlassian.commonmark:commonmark:0.13.1'

    testImplementation "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main'
    test.kotlin.srcDirs += 'src/test'
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = true
        csv.enabled = false
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/tmp/kdoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveName = "config4k-javadoc.jar"
    from "$buildDir/tmp/kdoc"
}

afterEvaluate {
    mavenSourcesJar.dependsOn javadocJar
}

publish {
    userOrg = 'config4k'
    repoName = 'config4k'
    groupId = 'io.github.config4k'
    artifactId = 'config4k'
    publishVersion = '0.4.1'
    description = 'A Typesafe Config wrapper for Kotlin'
    website = 'https://github.com/config4k/config4k'
    licences = ['Apache-2.0']
    issueTracker = 'https://github.com/config4k/config4k/issues'
    repository = 'https://github.com/config4k/config4k.git'
}
